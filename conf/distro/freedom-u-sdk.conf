DISTRO = "freedom-u-sdk"
DISTRO_NAME = "FreedomUSDK (SiFive Freedom Unleashed SDK)"
DISTRO_VERSION = "2024.10.00"
DISTRO_CODENAME = "2024November"
SDK_VENDOR = "-freedomusdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${METADATA_REVISION}', 'snapshot')}"
SDK_VERSION[vardepvalue] = "${SDK_VERSION}"

MAINTAINER = " SiFive <null@sifive.com>"

TARGET_VENDOR = "-freedomusdk"

LOCALCONF_VERSION = "2"

# Override these in FreedomUSDK based distros
FREEDOM_DEFAULT_DISTRO_FEATURES = "bluetooth opengl ptest multiarch pam systemd virtualization vulkan wifi"
FREEDOM_DEFAULT_EXTRA_RDEPENDS = "packagegroup-core-boot"
FREEDOM_DEFAULT_EXTRA_RRECOMMENDS = "kernel-module-af-packet"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${FREEDOM_DEFAULT_DISTRO_FEATURES}"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-sifive"

PREFERRED_PROVIDER_u-boot ?= "u-boot-sifive"
PREFERRED_PROVIDER_u-boot:qemuriscv64 = "u-boot-sifive"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-sifive"
PREFERRED_PROVIDER_virtual/bootloader:qemuriscv64 = "u-boot-sifive"

PREFERRED_PROVIDER_opensbi ?= "opensbi-sifive"

PREFERRED_PROVIDER_llvm = "clang"
PREFERRED_PROVIDER_llvm-native = "clang-native"
PREFERRED_PROVIDER_nativesdk-llvm = "nativesdk-clang"
PROVIDES:pn-clang = "llvm"
PROVIDES:pn-clang-native = "llvm-native"
PROVIDES:pn-nativesdk-clang = "nativesdk-llvm"

PREFERRED_VERSION_webkitgtk ?= "2.44.3"

SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATHINSTALL = "/opt/${DISTRO}/${SDK_VERSION}"

DISTRO_EXTRA_RDEPENDS += "${FREEDOM_DEFAULT_EXTRA_RDEPENDS}"
DISTRO_EXTRA_RRECOMMENDS += "${FREEDOM_DEFAULT_EXTRA_RRECOMMENDS}"

PACKAGE_CLASSES ?= "package_rpm"

SANITY_TESTED_DISTROS ?= " \
            poky-5.0 \n \
            poky-5.1 \n \
            ubuntu-20.04 \n \
            ubuntu-22.04 \n \
            ubuntu-24.04 \n \
            ubuntu-24.10 \n \
            fedora-38 \n \
            fedora-39 \n \
            fedora-40 \n \
            centosstream-8 \n \
            debian-11 \n \
            debian-12 \n \
            opensuseleap-15.4 \n \
            opensuseleap-15.5 \n \
            opensuseleap-15.6 \n \
            almalinux-8.8 \n \
            almalinux-9.2 \n \
            rocky-9 \n \
            "

require conf/distro/include/no-static-libs.inc
require conf/distro/include/yocto-uninative.inc
require conf/distro/include/security_flags.inc
require conf/distro/include/yocto-space-optimize.inc
INHERIT += "uninative"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"

FREEDOM_INIT_MANAGER = "systemd"
INIT_MANAGER ?= "${FREEDOM_INIT_MANAGER}"

# Enable creation of SPDX manifests by default
INHERIT += "create-spdx"

CLANGSDK = "1"

FORTRAN:forcevariable = ",fortran"

PREFERRED_PROVIDER_base-utils = "packagegroup-core-base-utils"
VIRTUAL-RUNTIME_base-utils = "packagegroup-core-base-utils"
VIRTUAL-RUNTIME_base-utils-hwclock = "util-linux-hwclock"
VIRTUAL-RUNTIME_base-utils-syslog = ""

VIRTUAL-RUNTIME_vim = "vim"

EXTRA_IMAGE_FEATURES:append = " package-management"

QEMU_TARGETS = "riscv64"

INITRAMFS_IMAGE ?= "demo-coreip-cli-initramfs"
INITRAMFS_IMAGE:qemuall ?= ""
INITRAMFS_IMAGE_BUNDLE ?= "1"
INITRAMFS_FSTYPES ?= "cpio.gz"

IMAGE_BOOT_FILES:append = " ${KERNEL_IMAGETYPE}-initramfs-${MACHINE}.bin"

INHERIT += "extrausers"
EXTRA_USERS_PARAMS = "usermod -p '\$6\$PWVNV6MfuO4pMdqO\$54BibXcgV/nXMrgbaMBioGHNDv1uGVFarQN9QnqM8IMOI/nEwnpB5noxJozigw0lObahcmc3lqTMPvLoSpXnP1' root;"
